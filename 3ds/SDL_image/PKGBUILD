# Maintainer:  Dave Murphy <davem@devkitpro.org>

pkgname=3ds-sdl_image
pkgver=1.2.12
pkgrel=1
pkgdesc="A simple library to load images of various formats as SDL surfaces (Nintendo 3DS port)"
arch=('any')
url="https://libsdl.org"
license=("LGPL")
options=(!strip libtool staticlibs)
depends=(
 '3ds-sdl'
 '3ds-libpng'
 '3ds-libjpeg-turbo'
)
makedepends=(
 '3ds-pkg-config'
 'devkitpro-pkgbuild-helpers'
)
source=("${url}/projects/SDL_image-${pkgver}.tar.gz")
sha256sums=(
'0b90722984561004de84847744d566809dbb9daf732a9e503b91a1b5a84e5699'
)

build() {
  cd SDL_image-$pkgver

  source ${DEVKITPRO}/devkitarm.sh
  source ${DEVKITPRO}/3dsvars.sh

  ./configure --prefix="${PORTLIBS_PREFIX}" --host=arm-none-eabi \
    --disable-shared --enable-static
  make
}

package() {
  cd SDL_image-$pkgver

  source ${DEVKITPRO}/devkitarm.sh
  source ${DEVKITPRO}/3dsvars.sh

  make DESTDIR="$pkgdir" install

}
