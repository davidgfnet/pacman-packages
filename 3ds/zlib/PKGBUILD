
# Maintainer: WinterMute <davem@devkitpro.org>
pkgname=3ds-zlib
pkgver=1.2.11
pkgrel=1
pkgdesc='Deflate compression method library (for Nintendo 3DS homebrew development)'
arch=('any')
url='http://www.zlib.net/'
license=('zlib')
options=(!strip libtool staticlibs)
source=("http://downloads.devkitpro.org/packages/sources/zlib-$pkgver.tar.xz")
sha256sums=('4ff941449631ace0d4d203e3483be9dbc9da454084111f97ea0a2114e19bf066')
makedepends=('devkitpro-pkgbuild-helpers')

build() {
  cd zlib-$pkgver

  source ${DEVKITPRO}/devkitarm.sh
  source ${DEVKITPRO}/3dsvars.sh

  CHOST=arm-none-eabi \
    ./configure --prefix="${PORTLIBS_PREFIX}" --static
  make libz.a # only build library, no programs
}

package() {
  cd zlib-$pkgver

  source ${DEVKITPRO}/devkitarm.sh
  source ${DEVKITPRO}/3dsvars.sh

  make DESTDIR="$pkgdir" install
  # license
  install -d "$pkgdir"${PORTLIBS_PREFIX}/licenses/$pkgname
  tail -n 31 README > "$pkgdir"${PORTLIBS_PREFIX}/licenses/$pkgname/LICENSE
  # remove useless stuff
  rm -r "$pkgdir"${PORTLIBS_PREFIX}/share
}
