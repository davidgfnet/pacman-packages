
# Maintainer: WinterMute <davem@devkitpro.org>
pkgname=3ds-libmad
pkgver=0.15.1b
pkgrel=1
pkgdesc='A high-quality MPEG audio decoder. (for Nintendo 3DS homebrew development)'
arch=('any')
url='https://www.underbit.com'
license=('GPL')
options=(!strip libtool staticlibs)
source=("https://sourceforge.net/projects/mad/files/libmad/${pkgver}/libmad-${pkgver}.tar.gz")
sha256sums=('bbfac3ed6bfbc2823d3775ebb931087371e142bb0e9bb1bee51a76a6e0078690')
makedepends=('3ds-pkg-config' 'devkitpro-pkgbuild-helpers')

build() {
  cd libmad-$pkgver

  source ${DEVKITPRO}/devkitarm.sh
  source ${DEVKITPRO}/3dsvars.sh

  ./configure --prefix="${PORTLIBS_PREFIX}" --host=arm-none-eabi --disable-shared --enable-static
  make
}

package() {
  cd libmad-$pkgver

  source ${DEVKITPRO}/devkitarm.sh
  source ${DEVKITPRO}/3dsvars.sh

  make DESTDIR="$pkgdir" install
}
